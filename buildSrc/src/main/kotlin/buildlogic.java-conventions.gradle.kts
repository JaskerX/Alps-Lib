plugins {
    `java-library`
    `maven-publish`
}

repositories {
    //mavenLocal() // NEVER use this in Production / Commits
    mavenCentral()

    maven {
        url = uri("https://mvn.alps-bte.com/repository/alps-bte/")
    }

    maven {
        url = uri("https://repo.papermc.io/repository/maven-public/")
    }

    maven {
        url = uri("https://jitpack.io/")
    }

    maven {
        url = uri("https://repo.codemc.io/repository/maven-public/")
    }

    maven {
        url = uri("https://repo.fancyinnovations.com/releases")
    }

    maven {
        url = uri("https://maven.enginehub.org/repo/")
    }
}

group = "com.alpsbte.alpslib"
java.sourceCompatibility = JavaVersion.VERSION_21

val ORG_GRADLE_PROJECT_alpsMavenUser: String? = project.findProperty("alpsMavenUser") as String?
val ORG_GRADLE_PROJECT_alpsMavenPassword: String? = project.findProperty("alpsMavenPassword") as String?

project.afterEvaluate {
    (project.extensions.getByName("publishing") as PublishingExtension).publications {
        if (tasks.jar.get().enabled) {
            create<MavenPublication>("maven") {
                from(project.components["java"])
            }
        }
    }
}


configure<PublishingExtension> {
    repositories {
        maven {
            credentials {
                username = ORG_GRADLE_PROJECT_alpsMavenUser
                password = ORG_GRADLE_PROJECT_alpsMavenPassword
            }
            url = uri("https://mvn.alps-bte.com/repository/alps-bte/")
        }
    }
}

tasks.withType<JavaCompile> {
    options.encoding = "UTF-8"
}

tasks.withType<Javadoc> {
    options.encoding = "UTF-8"
}

java {
    withSourcesJar()
    withJavadocJar()
}
